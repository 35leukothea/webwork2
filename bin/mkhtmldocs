#!/usr/bin/env perl

################################################################################
# WeBWorK mod_perl (c) 1995-2002 WeBWorK Team, Univeristy of Rochester
# $Id: mkhtmldocs,v 1.1 2003-11-19 18:46:53 sh002i Exp $
################################################################################

=head1 NAME

mkhtmldocs - use pod2html to generate HTML documentation

=cut

use strict;
use warnings;
use FindBin;

# set this to the URL of the htdocs/doc directory.
use constant DOC_BASE => "/webwork2_files/doc";
# FIXME: this should probably be read from global.conf.

sub main(@) {
	my $force = @_ && $_[0] eq "-f";
	
	my $lib = "$FindBin::Bin/../lib";
	my $htdocs = "$FindBin::Bin/../htdocs";
	my $doc = "$htdocs/doc";
	my $htmlroot = DOC_BASE;
	
	my @modules = `find $lib -name "*.pm"`;
	foreach my $module (@modules) {
		chomp $module;
		
		my $docfile = $module;
		$docfile =~ s/^$lib/$doc/;
		$docfile =~ s/\.pm$/.html/;
		
		next if not $force and -e $docfile and (stat $docfile)[9] >= (stat $module)[9];
		
		my ($docdir) = $docfile =~ m|^(.*)/|;
		unless (-e $docdir) {
			print "creating missing directory $docdir\n";
			system "mkdir -p $docdir"
				and die "mkdir failed: $!\n";
		}
		
		print "generating documentation for module $module\n";
		system "pod2html --htmlroot=$htmlroot --podroot=$lib --infile=$module --outfile=$docfile"
			and die "pod2html failed: $!\n";
	}
}

main(@ARGV);
